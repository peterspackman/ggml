#ifdef INPLACE
fn update(src_offset: u32, dst_offset: u32, scale: f32, bias: f32) {
    src[dst_offset] = scale * src[src_offset] + bias;
}

@group(0) @binding(1)
var<uniform> params: Params;
#else
fn update(src_offset: u32, dst_offset: u32, scale: f32, bias: f32) {
    dst[dst_offset] = scale * src[src_offset] + bias;
}

@group(0) @binding(1)
var<storage, read_write> dst: array<f32>;

@group(0) @binding(2)
var<uniform> params: Params;
#endif

struct Params {
    offset_src: u32, // in elements
    offset_dst: u32, // in elements

    // Strides (in elements)
    stride_src1: u32,
    stride_src2: u32,
    stride_src3: u32,

    stride_dst1: u32,
    stride_dst2: u32,
    stride_dst3: u32,

    // Shape of src/dst
    ne0: u32,
    ne1: u32,
    ne2: u32,
    ne3: u32,

    eps: f32
};

@group(0) @binding(0)
var<storage, read_write> src: array<f32>;

var<workgroup> scratch: array<f32, WG_SIZE>;
#ifdef NORM
var<workgroup> scratch_sum: array<f32, WG_SIZE>;
#endif

@compute @workgroup_size(WG_SIZE)
fn main(@builtin(workgroup_id) wid: vec3<u32>,
        @builtin(local_invocation_id) lid: vec3<u32>) {

    // one thread per row
    var i = wid.x;
    let i3 = i / (params.ne2 * params.ne1);
    i = i % (params.ne2 * params.ne1);
    let i2 = i / params.ne1;
    let i1 = i % params.ne1;
    let i_src_row = params.offset_src + i3 * params.stride_src3 + i2 * params.stride_src2 + i1 * params.stride_src1;
    let i_dst_row = params.offset_dst + i3 * params.stride_dst3 + i2 * params.stride_dst2 + i1 * params.stride_dst1;

    let elems = (params.ne0 + WG_SIZE - 1) / WG_SIZE;

    var sum_sq = 0.0f;
#ifdef NORM
    var sum = 0.0f;
#endif
    var col = lid.x;
    for (var j: u32 = 0; j < elems; j++) {
        if (col >= params.ne0) {
            break;
        }
        let v = src[i_src_row + col];
        sum_sq += v * v;
#ifdef NORM
        sum += v;
#endif
        col += WG_SIZE;
    }

    scratch[lid.x] = sum_sq;
#ifdef NORM
    scratch_sum[lid.x] = sum;
#endif
    workgroupBarrier();
    var red_off: u32 = WG_SIZE / 2;
    while (red_off > 0) {
        if (lid.x < red_off) {
            scratch[lid.x] += scratch[lid.x + red_off];
#ifdef NORM
            scratch_sum[lid.x] += scratch_sum[lid.x + red_off];
#endif
        }
        red_off = red_off / 2;
        workgroupBarrier();
    }
    sum_sq = scratch[0];
#ifdef NORM
    sum = scratch_sum[0];
#endif

#ifdef RMS_NORM
    let scale = 1.0/sqrt(sum_sq/f32(params.ne0) + params.eps);
    let bias  = 0.0;
#elif defined(L2_NORM)
    let scale = 1.0/max(sqrt(sum_sq), params.eps);
    let bias  = 0.0;
#elif defined(NORM)
    let mean  = sum / f32(params.ne0);
    let var_  = sum_sq / f32(params.ne0) - mean * mean;
    let scale = 1.0/sqrt(var_ + params.eps);
    let bias  = -mean * scale;
#endif

    col = lid.x;
    for (var j: u32 = 0; j < elems; j++) {
        if (col >= params.ne0) {
            break;
        }
        update(i_src_row + col, i_dst_row + col, scale, bias);
        col += WG_SIZE;
    }
}
